buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:1.0.0'
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
  }
}

apply plugin: 'android'
apply plugin: 'android-apt'

repositories {
  mavenCentral()
}

dependencies {
  compile 'com.android.support:support-v4:21.0.+'
  compile 'com.squareup.retrofit:retrofit:1.9.+'
  compile 'com.squareup.picasso:picasso:2.5.+'
  compile 'com.squareup.okhttp:okhttp:2.3.+'
  compile 'com.squareup.mimecraft:mimecraft:1.1.+'
  compile('com.octo.android.robospice:robospice:1.4.+') {
    exclude group: 'org.apache.commons', module: 'commons-io'
  }
  compile('com.octo.android.robospice:robospice-retrofit:1.4.+') {
    exclude group: 'org.apache.commons', module: 'commons-io'
  }
  apt 'org.androidannotations:androidannotations:3.2'
  compile 'org.androidannotations:androidannotations-api:3.2'
  compile 'commons-io:commons-io:2.4'
  compile 'org.apache.tika:tika-core:1.7'
  compile 'com.samskivert:jmustache:1.10'
  compile 'com.readystatesoftware.systembartint:systembartint:1.0.+'
  compile 'com.github.chrisbanes.actionbarpulltorefresh:library:0.9.+'
  compile 'com.github.johnkil.android-robototextview:robototextview:2.3.+'
}

apt {
  arguments {
    androidManifestFile variant.outputs[0].processResources.manifestFile
    resourcePackageName android.defaultConfig.applicationId
  }
}

android {
  compileSdkVersion 22
  buildToolsVersion '22.0.1'

  defaultConfig {
    applicationId 'com.floatboth.antigravity'
    versionCode 7
    versionName '1.4.0-SNAPSHOT'
    minSdkVersion 14
    targetSdkVersion 22
  }

  buildTypes {
    release {
      minifyEnabled true
      proguardFile rootProject.file('proguard.cfg')
    }
  }

  sourceSets {
    main {
      manifest.srcFile 'AndroidManifest.xml'
      res.srcDirs = ['res']
      assets.srcDirs = ['assets']
    }
  }

  packagingOptions {
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/NOTICE.txt'
  }

  File signFile = rootProject.file('release-signing.properties')
  if (signFile.exists()) {
    Properties p = new Properties()
    p.load(new FileInputStream(signFile))
    signingConfigs {
      releaseConfig {
        storeFile new File(p.storeFile)
        storePassword p.storePassword
        keyAlias p.keyAlias
        keyPassword p.keyPassword
      }
    }
    buildTypes.release.signingConfig signingConfigs.releaseConfig
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.3'
}
